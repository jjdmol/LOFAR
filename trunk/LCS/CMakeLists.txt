
## ------------------------------------------------------------------------------
## Project characteristics

project (LCS)

cmake_minimum_required (VERSION 2.5)

## ------------------------------------------------------------------------------
## Set CMAKE_MODULE_PATH to load custom CMake modules

find_path (USG_ROOT cmake/CMakeSettings.cmake
  PATHS 
  ${LCS_SOURCE_DIR}
  ${LCS_SOURCE_DIR}/..
  ${LCS_SOURCE_DIR}/../..
  ${LCS_SOURCE_DIR}/../../..
  $ENV{LOFARSOFT}
  )

if (USG_ROOT)
  include (${USG_ROOT}/cmake/CMakeSettings.cmake)
else (USG_ROOT)
  message (FATAL_ERROR "Unable to locate additional CMake scripts!")
endif (USG_ROOT)

## ------------------------------------------------------------------------------
## Additional CMake modules

include (CheckIncludeFiles)
include (CheckLibraryExists)
include (CheckTypeSize)
include (CTest)
enable_testing()

## Custom modules

include (MakePackageVersion)

## ------------------------------------------------------------------------------
## Check system libraries

check_include_files (sys/socket.h  HAVE_SYS_SOCKET_H )
check_include_files (sys/types.h   HAVE_SYS_TYPES_H  )
check_include_files (unistd.h      HAVE_UNISTD_H     )
check_include_files (stdarg.h      HAVE_STDARG_H     )
check_include_files (time.h        HAVE_TIME_H       )
check_include_files (stdio.h       HAVE_STDIO_H      )
check_include_files (netinet/in.h  HAVE_NETINET_IN_H )

if (HAVE_SYS_TYPES_H)
  check_type_size ("uint" HAVE_UINT)
  if (HAVE_UINT)
    add_definitions (-DHAVE_UINT)
  endif (HAVE_UINT)
  ##
  check_type_size ("long" HAVE_LONG)
  if (HAVE_LONG)
    add_definitions (-DHAVE_LONG)
  endif (HAVE_LONG)
  ##
  check_type_size ("long long" HAVE_LONGLONG)
  if (HAVE_LONGLONG)
    add_definitions (-DHAVE_LONGLONG)
  endif (HAVE_LONGLONG)
  ##
  check_type_size ("ushort" HAVE_USHORT)
  if (HAVE_USHORT)
    add_definitions (-DHAVE_USHORT)
  endif (HAVE_USHORT)
else (HAVE_SYS_TYPES_H)
  message (STATUS "Unable to find sys/types.h")
endif (HAVE_SYS_TYPES_H)

## ------------------------------------------------------------------------------
## Check for required tools

find_program ( bison_bin bison )
find_program ( flex_bin  flex  )
find_program ( yacc_bin  yacc  )

## ------------------------------------------------------------------------------
## Search for external libraries

## Boost++

include (FindBoost)

if (BOOST_INCLUDES)
  include_directories (${BOOST_INCLUDES})
  add_definitions (-DHAVE_BOOST)
endif (BOOST_INCLUDES)

## casacore

include (FindCASACORE)

if (CASACORE_INCLUDES)
  include_directories (${CASACORE_INCLUDES})
  add_definitions (-DHAVE_AIPSPP)
endif (CASACORE_INCLUDES)


## Commonly used configuration files

set (LCS_config ${LCS_BINARY_DIR}/lofar_config.h)

file (WRITE ${LCS_config} "/* lofar_config.h --  Generated by CMake. */\n\n")

include_directories (${LCS_BINARY_DIR})

## ------------------------------------------------------------------------------
## Module header files

include_directories (
  ${LCS_SOURCE_DIR}/Blob/include
  ${LCS_SOURCE_DIR}/Common/include
  ${LCS_SOURCE_DIR}/ACC/ALC/include
  ${LCS_SOURCE_DIR}/ACC/APS/include
  ${LCS_SOURCE_DIR}/ACC/PLC/include
  ${LCS_SOURCE_DIR}/Transport/include
  )

## ------------------------------------------------------------------------------
## Directories to be included in the build

#add_subdirectory (ACC)
add_subdirectory (AMC)
add_subdirectory (Blob)
add_subdirectory (Common)
add_subdirectory (LACE)
#add_subdirectory (Math)
add_subdirectory (Tools)
add_subdirectory (Transport)


## ==============================================================================
##
## Feedback on configuration settings
##
## ==============================================================================

message (STATUS "Flex  = ${flex_bin}")
message (STATUS "Bison = ${bison_bin}")
message (STATUS "Yacc  = ${yacc_bin}")
