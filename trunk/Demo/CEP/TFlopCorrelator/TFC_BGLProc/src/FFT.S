#if defined HAVE_BGL

_constants:
	# X, X
	.long	0, 0, 0, 0

	# X, cos(4*PI/16)
	.long	0, 0, 0x3FE6A09E, 0x667F3BCD

	# sin(6*PI/16), -sin(6*PI/16)
	.long	0x3FED906B, 0xCF328D46, 0xBFED906B, 0xCF328D46

	# sin(2*PI/16) - sin(6*PI/16), X
	.long	0xBFE1517A, 0x7BDB3894, 0, 0

	# sin(2*PI/16) + sin(6*PI/16), X
	.long	0xBFF4E7AE, 0x9144F0FC, 0, 0

	# cos(6*PI/16), cos(6*PI/16)
	.long	0x3FD87DE2, 0xA6AEA964, 0x3FD87DE2, 0xA6AEA964

	# cos(2*PI/16) + cos(6*PI/16), cos(2*PI/16) + cos(6*PI/16)
	.long	0x3FF4E7AE, 0x9144F0FC, 0x3FF4E7AE, 0x9144F0FC

	# -cos(2*PI/16) + cos(6*PI/16), -cos(2*PI/16) + cos(6*PI/16)
	.long	0xBFE1517A, 0x7BDB3894, 0xBFE1517A, 0x7BDB3894

	# X, 1
	.long	0, 0, 0x3FF00000, 0


.global	_fft_16
_fft_16:
#	r3 : input ptr
#	r4 : input stride
#	r5 : output ptr
#	r6 : output stride

	lis	7,_constants@ha
	addi	7,7,_constants@l
	li	8,16

#define C1 24
#define C2 25
#define C3 26
#define C4 27
#define C5 28
#define C6 29
#define C7 30
#define C8 31
#define C9 23
	lfpdx	C1,0,7
	lfpdux	C2,7,8
	lfpdux	C3,7,8
	lfpdux	C4,7,8
	lfpdux	C5,7,8
	lfpdux	C6,7,8
	lfpdux	C7,7,8
	lfpdux	C8,7,8
	lfpdux	C9,7,8

#define a0 10
#define a1 8
#define a2 3
#define a3 15
#define a4 4
#define a5 5
#define a6 12
#define a7 0
#define a8 16
#define a9 9
#define a10 7
#define a11 1
#define a12 18
#define a13 2
#define a14 13
#define a15 11
	lfpsx	a0,0,3
	lfpsux	a1,3,4
	lfpsux	a2,3,4
	lfpsux	a3,3,4
	lfpsux	a4,3,4
	lfpsux	a5,3,4
	lfpsux	a6,3,4
	lfpsux	a7,3,4
	lfpsux	a8,3,4
	lfpsux	a9,3,4
	lfpsux	a10,3,4
	lfpsux	a11,3,4
	lfpsux	a12,3,4
	lfpsux	a13,3,4
	lfpsux	a14,3,4
	lfpsux	a15,3,4

#define b1 6
#define b2 16
#define b3 10
#define b4 18
#define b5 14
#define b6 7
#define b7 4
#define b8 13
#define b9 12
#define b10 9
#define b11 3
#define b12 2
#define b13 5
#define b14 1
#define b15 15
#define b16 11
	fpadd	b1,a0,a8	# BFEFDGFG DGFGFG.H E.C..... ........
	fpsub	b2,a0,a8	# BFEFDGFG DGFGFG.H D.C..... ........
	fpadd	b3,a4,a12	# BFEFDGFG DGEGFG.H D.C..... ........
	fpsub	b4,a4,a12	# BFEFDGFG DGEGFG.H D.B..... ........
	fpadd	b5,a2,a10	# BFEF.GFG DGEGFGFH D.B..... ........
	fpsub	b6,a2,a10	# BFEF.GFF DGEGFGFH D.B..... ........
	fpadd	b7,a6,a14	# BFE.CGFF DGEGFGFH D.B..... ........
	fpsub	b8,a6,a14	# BFE.CGFF DGEGFFFH D.B..... ........
	fpadd	b9,a1,a9	# BFE.CGFF DGEGEFFH D.B..... ........
	fpsub	b10,a1,a9	# BFE.CGFF DFEGEFFH D.B..... ........
	fpadd	b11,a5,a13	# BFEECGFF .FEGEFFH D.B..... ........
	fpsub	b12,a5,a13	# BFDECGFF .FEGEFFH D.B..... ........
	fpadd	b13,a3,a11	# BFDECFFF .FEGEFFH D.B..... ........
	fpsub	b14,a3,a11	# BEDECFFF .FEGEFFH D.B..... ........
	fpadd	b15,a7,a15	# BEDECFFF .FEGEFFG D.B..... ........
	fpsub	b16,a7,a15	# BEDECFFF .FEFEFFG D.B..... ........

#define c1 8
#define c2 10
#define c3 6
#define c4 4
#define c5 14
#define c6 3
#define c7 12
#define c8 15
#define c9 17
#define c10 13
#define c11 5
#define c12 11
#define c13 9
#define c14 1
	fpadd	c1,b1,b3	# .EDECFFF CFEFEFFG D.B..... ........
	fpsub	c2,b1,b3	# .EDECFFF CFDFEFFG D.B..... ........
	fpadd	c3,b5,b7	# .EDECFEF CFDFEFFG D.B..... ........
	fpsub	c4,b5,b7	# .EDEBFEF CFDFEFFG D.B..... ........
	fpadd	c5,b9,b11	# .EDEBFEF CFDFEFEG D.B..... ........
	fpsub	c6,b9,b11	# .EDDBFEF CFDFEFEG D.B..... ........
	fpadd	c7,b13,b15	# .EDDBFEF CFDFDFEG D.B..... ........
	fpsub	c8,b13,b15	# .EDDBFEF CFDFDFEF D.B..... ........
	fpadd	c9,b6,b8	# .EDDB.EF CFDFDFEF DDB..... ........
	fpsub	c10,b6,b8	# .EDDB.EF CFDFDEEF DDB..... ........
	fpadd	c11,b10,b16	# .EDDBEE. CFDFDEEF DDB..... ........
	fpsub	c12,b10,b16	# .EDDBEE. CFDEDEEF DDB..... ........
	fpadd	c13,b12,b14	# .EDDBEE. CEDEDEEF DDB..... ........
	fpsub	c14,b12,b14	# .DDDBEE. CEDEDEEF DDB..... ........

#define d1 7
#define d2 6
#define d3 8
#define d4 12
#define d5 14
#define d6 15
#define d7 3
#define d8 2
	fpadd	d1,c1,c3	# .D.DBEEE CEDEDEEF DDB..... ........
	fpsub	d2,c1,c3	# .D.DBEDE CEDEDEEF DDB..... ........
	fpadd	d3,c5,c7	# .D.DBEDE BEDEDEEF DDB..... ........
	fpsub	d4,c5,c7	# .D.DBEDE BEDECEEF DDB..... ........
	fpadd	d5,c6,c8	# .D.DBEDE BEDECEDF DDB..... ........
	fpsub	d6,c6,c8	# .D.DBEDE BEDECEDE DDB..... ........
	fpadd	d7,c11,c13	# .D.CBEDE BEDECEDE DDB..... ........
	fpsub	d8,c11,c13	# .DCCBEDE BEDECEDE DDB..... ........

#define e1 0
#define e2 8
#define e6 7
#define e10 18
#define e13 3
#define e16 2
	fpadd	e1,d1,d3	# ADCCBEDE BEDECEDE DDB..... ........
	fpsub	e2,d1,d3	# ADCCBEDE AEDECEDE DDB..... ........
	fxmr	e6,c4		# ADCCBECD AECEBEDE DDB..... ........
	fsneg	e6,e6		# ADCC.ECD AECEBEDE DDB..... ........
	fxmr	e10,b4		# ADCC.ECD AECEBECD CDA..... ........
	fsneg	e10,e10		# ADCC.ECD AECEBECD CDA..... ........
	fxmr	e13,d7		# ADCB.ECD AECEBDCD CCA..... ........
	fpmul	e13,C3,e13	# ADCB.ECD AECEBDCD CCA..... ........
	fpmul	e16,C6,d8	# ADBB.DCD ADCEBDCD CCA..... ........

#define f1 4
#define f2 12
#define f3 6
#define f4 14
#define f5 10
#define f6 7
#define f7 15
#define f8 13
#define f9 16
#define f10 17
#define f11 5
#define f12 9
#define f13 11
#define f14 1
	fxcxnsma f1,C9,d4,d2	# ACBBADCD ADCDBDCD CCA..... ........
	fxcxnpma f2,C9,d4,d2	# ACBBADCD ADCDADCD CCA..... ........
	fxcxnsma f3,C2,d5,c2	# ACBBADBD ADCDADCD CCA..... ........
	fxcxnpma f4,C2,d5,c2	# ACBBADBD ADCDADBD CCA..... ........
	fxcsmadd f5,C2,d6,e6	# ACBBADBD ADBDADBD CCA..... ........
	fxcsnmsub f6,C2,d6,e6	# ACBBADBC ADBDADBD CCA..... ........
	fxcsmadd f7,C2,c10,b2	# ACBBADBC ADBDADBC CCA..... ........
	fxcsnmsub f8,C2,c10,b2	# ACBBADBC ADBDACBC CCA..... ........
	fxcxnsma f9,C2,c9,e10	# ACBBADBC ADBDACBC BCA..... ........
	fxcxnpma f10,C2,c9,e10	# ACBBADBC ADBDACBC BBA..... ........
	fxcpnsma f11,C4,c11,e13	# ACBBACBC ADBDACBC BB...... ........
	fxcpnsma f12,C5,c13,e13	# ACBBACBC ACBDACBC BB...... ........
	fpmsub	f13,C7,c12,e16	# ACB.ACBC ACBCACBC BB...... ........
	fpmsub	f14,C8,c14,e16	# ABB.ACBC ACBCACBC BB...... ........

#define g1 2
#define g2 6
#define g3 10
#define g4 14
#define g5 7
#define g6 15
#define g7 5
#define g8 13
#define g9 9
#define g10 16
#define g11 11
#define g12 17
	fpadd	g1,f3,f5	# ABA.ACBC ACBCACBC BB...... ........
	fpsub	g2,f3,f5	# ABA.ACAC ACBCACBC BB...... ........
	fpadd	g3,f4,f6	# ABA.ACAC ACACACBC BB...... ........
	fpsub	g4,f4,f6	# ABA.ACAC ACACACAC BB...... ........
	fpadd	g5,f7,f11	# ABA.ACAB ACACACAC BB...... ........
	fpsub	g6,f7,f11	# ABA.ACAB ACACACAB BB...... ........
	fpadd	g7,f8,f12	# ABA.ABAB ACACACAB BB...... ........
	fpsub	g8,f8,f12	# ABA.ABAB ACACABAB BB...... ........
	fpadd	g9,f9,f13	# ABA.ABAB ABACABAB BB...... ........
	fpsub	g10,f9,f13	# ABA.ABAB ABACABAB AB...... ........
	fpadd	g11,f10,f14	# ABA.ABAB ABABABAB AB...... ........
	fpsub	g12,f10,f14	# ABA.ABAB ABABABAB AA...... ........

#define x1 1
#define x3 3
#define x5 5
#define x7 7
#define x9 9
#define x11 11
#define x13 13
#define x15 15
	fpadd	x1,g5,g9	# AAA.ABAB ABABABAB AA...... ........
	fpsub	x3,g7,g11	# AAAAABAB ABABABAB AA...... ........
	fpadd	x5,g7,g11	# AAAAAAAB ABABABAB AA...... ........
	fpsub	x7,g5,g9	# AAAAAAAA ABA.ABAB AA...... ........
	fpadd	x9,g6,g10	# AAAAAAAA AAA.ABAB AA...... ........
	fpsub	x11,g8,g12	# AAAAAAAA AAAAABAB AA...... ........
	fpadd	x13,g8,g12	# AAAAAAAA AAAAAAAB A....... ........
	fpsub	x15,g6,g10	# AAAAAAAA AAAAAAAA A....... ........

	stfpsx	e1,0,5		# AAAAAAAA AAAAAAAA ........ ........
	stfpsux x1,5,6		# .AAAAAAA AAAAAAAA ........ ........
	stfpsux g1,5,6		# ..AAAAAA AAAAAAAA ........ ........
	stfpsux x3,5,6		# ...AAAAA AAAAAAAA ........ ........
	stfpsux f1,5,6		# ....AAAA AAAAAAAA ........ ........
	stfpsux x5,5,6		# .....AAA AAAAAAAA ........ ........
	stfpsux g2,5,6		# ......AA AAAAAAAA ........ ........
	stfpsux x7,5,6		# .......A AAAAAAAA ........ ........
	stfpsux e2,5,6		# ........ AAAAAAAA ........ ........
	stfpsux x9,5,6		# ........ .AAAAAAA ........ ........
	stfpsux	g3,5,6		# ........ ..AAAAAA ........ ........
	stfpsux x11,5,6		# ........ ...AAAAA ........ ........
	stfpsux	f2,5,6		# ........ ....AAAA ........ ........
	stfpsux x13,5,6		# ........ .....AAA ........ ........
	stfpsux g4,5,6		# ........ ......AA ........ ........
	stfpsux x15,5,6		# ........ .......A ........ ........


	blr
#endif
