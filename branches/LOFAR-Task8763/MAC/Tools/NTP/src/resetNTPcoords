#!/bin/csh -f
#
# Script: reserNTPcoords.csh
#
# This script sets the station coordinates from the file 
# /opt/lofar/etc/StaticMetaData/StationInfo.dat in the file
# /etc/ntp.oncore.0. These coordinates are used as initial guess for 
# determining the GPS location of a station.
#
# Usage: reserNTPcoords.csh localhost or <stationname>
#
# Should be run as root, or writing the /etc/ntp.oncore.0 file will fail
#

set ntpfile = /etc/ntp.oncore.0

# Only run if file ntp.oncore.0 has no valid coordinates (LAT 0, LON 0, HT 0)
if (! (-e $ntpfile)) then 
  echo "Cannot find file $ntpfile"
  exit 1
endif

set lat = `grep "^LAT" $ntpfile| awk '{print $2}'`
set lon = `grep "^LON" $ntpfile| awk '{print $2}'`
set height = `grep "^HT" $ntpfile| awk '{print $2}'`

if ( !($lat == 0 && $lon == 0 && $height == 0)) then
  echo "Values for LAT, LON, HT already provided in file $ntpfile"
  exit 1
endif

# 
if ($1 == "") then 
  echo "Provide a stationname, or localhost"
  exit 1
endif

if ($1 == "localhost") then 
    set ntphost = $HOSTNAME
else 
    set ntphost = $1
endif

set ntphost = `echo $ntphost|awk '{print substr($1,0,5)}'`

# Look up host in file /opt/lofar/etc/StaticMetaData/StationInfo.dat

if (! (-e /opt/lofar/etc/StaticMetaData/StationInfo.dat)) then 
  echo "Could not find file /opt/lofar/etc/StaticMetaData/StationInfo.dat"
  exit 1
endif

set st_info = ( `grep ^$ntphost /opt/lofar/etc/StaticMetaData/StationInfo.dat` )
if ($#st_info == 0) then 
  echo "Cannot find station $ntphost in file /opt/lofar/etc/StaticMetaData/StationInfo.dat"
  exit 1
endif

# Extract coordinates from file (columns 3,4,5)
set st_lon = $st_info[4]
set st_lat = $st_info[5]
set st_height = $st_info[6]

# Recalculate into format needed by file ntp.oncore.0

set st_lon_deg = `echo $st_lon | awk -F. '{print $1}'`
set st_lon_dec = `echo $st_lon | awk -F. '{print $2}'`
set st_lon_dec = `echo "0."$st_lon_dec "* 60" | bc`
set new_lon = 'LON\ '$st_lon_deg'\ '$st_lon_dec

set st_lat_deg = `echo $st_lat | awk -F. '{print $1}'`
set st_lat_dec = `echo $st_lat | awk -F. '{print $2}'`
set st_lat_dec = `echo "0."$st_lat_dec "* 60" | bc`
set new_lat = 'LAT\ '$st_lat_deg'\ '$st_lat_dec

set new_height = 'HT\ \ '$st_height'\ m'

# Copy old file for safeguarding and replace values
cp -f $ntpfile $ntpfile.org

sed -i s/"LAT\ 0"/"${new_lat}"/g $ntpfile
sed -i s/"LON\ 0"/"${new_lon}"/g $ntpfile
sed -i s/"HT\ \ 0"/"${new_height}"/g $ntpfile


