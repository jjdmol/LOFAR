
## ------------------------------------------------------------------------------
## Project characteristics

project (LOFAR)

cmake_minimum_required (VERSION 2.5)

## ------------------------------------------------------------------------------
## Set CMAKE_MODULE_PATH to load custom CMake modules

find_path (USG_ROOT cmake/CMakeSettings.cmake
  PATHS 
  ${LOFAR_SOURCE_DIR}
  ${LOFAR_SOURCE_DIR}/..
  ${LOFAR_SOURCE_DIR}/../..
  ${LOFAR_SOURCE_DIR}/../../..
  $ENV{LOFARSOFT}
  )

if (USG_ROOT)
  include (${USG_ROOT}/cmake/CMakeSettings.cmake)
else (USG_ROOT)
  message (FATAL_ERROR "Unable to locate additional CMake scripts!")
endif (USG_ROOT)

## ------------------------------------------------------------------------------

option (LOFAR_VERBOSE_CONFIGURE "Verbose output during configuration?" 0)
option (LOFAR_BUILD_TESTS       "Build the test programs?"             1)
option (LOFAR_WITH_TINYCEP      "Use tinyCEP?"                         0)

## ------------------------------------------------------------------------------
## Additional CMake modules

include (CheckIncludeFiles)
include (CheckLibraryExists)
#include (CheckFunctionExists)
include (CheckTypeSize)
include (CTest)
enable_testing()

## ------------------------------------------------------------------------------
## Check system libraries

check_include_files (sys/socket.h  HAVE_SYS_SOCKET_H )
check_include_files (sys/types.h   HAVE_SYS_TYPES_H  )
check_include_files (unistd.h      HAVE_UNISTD_H     )
check_include_files (stdarg.h      HAVE_STDARG_H     )
check_include_files (time.h        HAVE_TIME_H       )
check_include_files (stdio.h       HAVE_STDIO_H      )
check_include_files (netinet/in.h  HAVE_NETINET_IN_H )

if (HAVE_SYS_TYPES_H)
  check_type_size ("uint" HAVE_UINT)
  if (HAVE_UINT)
    add_definitions (-DHAVE_UINT)
  endif (HAVE_UINT)
  ##
  check_type_size ("long" HAVE_LONG)
  if (HAVE_LONG)
    add_definitions (-DHAVE_LONG)
  endif (HAVE_LONG)
  ##
  check_type_size ("long long" HAVE_LONGLONG)
  if (HAVE_LONGLONG)
    add_definitions (-DHAVE_LONGLONG)
  endif (HAVE_LONGLONG)
  ##
  check_type_size ("ushort" HAVE_USHORT)
  if (HAVE_USHORT)
    add_definitions (-DHAVE_USHORT)
  endif (HAVE_USHORT)
else (HAVE_SYS_TYPES_H)
  message (STATUS "Unable to find sys/types.h")
endif (HAVE_SYS_TYPES_H)

## ------------------------------------------------------------------------------
## Check for required tools

find_program ( bison_bin bison )
find_program ( flex_bin  flex  )
find_program ( lex_bin   lex   )
find_program ( yacc_bin  yacc  )

## ------------------------------------------------------------------------------
## Search for external libraries

if (NOT LOFAR_VERBOSE_CONFIGURE)
  set (BOOST_FIND_QUIETLY TRUE)
  set (CASACORE_FIND_QUIETLY TRUE)
endif (NOT LOFAR_VERBOSE_CONFIGURE)

## Boost++

include (FindBoost)

if (BOOST_INCLUDES)
  include_directories (${BOOST_INCLUDES})
  add_definitions (-DHAVE_BOOST)
endif (BOOST_INCLUDES)

## casacore

include (FindCASACORE)

if (CASACORE_INCLUDES)
  include_directories (${CASACORE_INCLUDES})
  add_definitions (-DHAVE_AIPSPP)
endif (CASACORE_INCLUDES)

## Commonly used configuration files

set (LOFAR_config ${LOFAR_BINARY_DIR}/lofar_config.h)

file (WRITE ${LOFAR_config} "/* lofar_config.h --  Generated by CMake. */\n\n")

include_directories (${LOFAR_BINARY_DIR})

## Common compiler flags

add_definitions (-fPIC)

## ------------------------------------------------------------------------------
## Module header files

include_directories (
  ## LCS
  ${LOFAR_SOURCE_DIR}/LCS/Blob/include
  ${LOFAR_SOURCE_DIR}/LCS/Common/include
  ${LOFAR_SOURCE_DIR}/LCS/ACC/ALC/include
  ${LOFAR_SOURCE_DIR}/LCS/ACC/APS/include
  ${LOFAR_SOURCE_DIR}/LCS/ACC/PLC/include
  ${LOFAR_SOURCE_DIR}/LCS/AMC/AMCBase/include
  ${LOFAR_SOURCE_DIR}/LCS/AMC/AMCImpl/include
  ${LOFAR_SOURCE_DIR}/LCS/Transport/include
  ## CEP
  ${LOFAR_SOURCE_DIR}/CEP/CEPFrame/include
  ${LOFAR_SOURCE_DIR}/CEP/tinyCEP/include
  ## Appl
  ${LOFAR_SOURCE_DIR}/Appl/ApplCommon/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_BandpassCorrector/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_BBS/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_BinningFlagger/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_DataSquasher/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_DFTImager/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_Flagger/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_Flagger2/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_FrequencyFlagger/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_Generator/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_Imager/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_Interface/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_pp_lib/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_SPWCombine/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_Storage/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_Tools/include
  ${LOFAR_SOURCE_DIR}/Appl/CEP/CS1/CS1_IONProc/src
  )

## ------------------------------------------------------------------------------
## Check for the presence of various packages in the code tree

## --- Appl --------------------------------------

find_path (HAVE_APPLCOMMON CMakeLists.txt ${LOFAR_SOURCE_DIR}/Appl/ApplCommon)

if (HAVE_APPLCOMMON)
   set (HAVE_APPLCOMMON TRUE CACHE BOOL "Have package ApplCommon?" FORCE)
endif (HAVE_APPLCOMMON)

## --- CEP ---------------------------------------

find_path (HAVE_CEPFRAME CMakeLists.txt ${LOFAR_SOURCE_DIR}/CEP/CEPFrame)

if (HAVE_CEPFRAME)
   set (HAVE_CEPFRAME TRUE CACHE BOOL "Have package CEPFrame?" FORCE)
endif (HAVE_CEPFRAME)

find_path (HAVE_TINYCEP CMakeLists.txt ${LOFAR_SOURCE_DIR}/CEP/tinyCEP)

if (HAVE_TINYCEP)
   set (HAVE_TINYCEP TRUE CACHE BOOL "Have package TINYCEP?" FORCE)
endif (HAVE_TINYCEP)

## --- LCS ---------------------------------------

find_path (HAVE_APS CMakeLists.txt ${LOFAR_SOURCE_DIR}/LCS/ACC/APS)

if (HAVE_APS)
   set (HAVE_APS TRUE CACHE BOOL "Have package APS?" FORCE)
endif (HAVE_APS)

## ------------------------------------------------------------------------------
## Directories to be included in the build

add_subdirectory (Appl)

if (HAVE_CEPFRAME AND HAVE_TINYCEP)
  add_subdirectory (CEP)
endif (HAVE_CEPFRAME AND HAVE_TINYCEP)

if (HAVE_APS)
  add_subdirectory (LCS)
endif (HAVE_APS)

## ==============================================================================
##
## Feedback on configuration settings
##
## ==============================================================================

if (LOFAR_VERBOSE_CONFIGURE)
  message (STATUS "-----------------------------------------------------------------")
  message (STATUS "[LOFAR] Configuration summary")
  message (STATUS "LOFAR_SOURCE_DIR        = ${LOFAR_SOURCE_DIR}")
  message (STATUS "CMAKE_MODULE_PATH       = ${CMAKE_MODULE_PATH}")
  message (STATUS "Have package ApplCommon = ${HAVE_APPLCOMMON}")
  message (STATUS "Have package APS        = ${HAVE_APS}")
  message (STATUS "Have package CEPFrame   = ${HAVE_CEPFRAME}")
  message (STATUS "Have package tinyCEP    = ${HAVE_TINYCEP}")
  message (STATUS "Boost++ includes        = ${BOOST_INCLUDES}")
  message (STATUS "Boost++ libraries       = ${BOOST_LIBRARIES}")
  message (STATUS "Have Boost++            = ${HAVE_BOOST}")
  message (STATUS "Have casacore           = ${HAVE_CASACORE}")
  message (STATUS "-----------------------------------------------------------------")
endif (LOFAR_VERBOSE_CONFIGURE)
